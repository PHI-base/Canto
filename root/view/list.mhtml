<%args>
$rs
$config_name
$list_title
$disclosed => 1  # true will make the list default to visible
$is_inline
</%args>

<div class="sect">
% if ($list_title ne '') {
<span class="sect-title <% $disclosed_style %>"><a href="#"> <% $list_title %> </a> </span>
% }
<div class="sect-content">
% if (@rows == 0) {
    [none]
% } else {
  <table class="list">
    <thead>
      <tr>
%   for my $col_conf (@column_confs) {
%     my $column_name = $col_conf->{name};
%     my $column_heading = ucfirst $column_name;
%     $column_heading =~ s/_/ /g;
        <th><span class="field-label">
%     if ($is_inline) {
          <% $column_heading %>
%     } else {
          <a href="<% $c->request->uri_with({order_by => '<' . $column_name}) %>"><% $column_heading %></a>
%     }
        </span></th>
%   }
      </tr>
    </thead>
%   for my $object (@rows) {
    <tr>
%     for my $col_conf (@column_confs) {
%       my $align_attr = '';
%       if (defined $col_conf->{format} && $col_conf->{format} eq 'integer' ||
%           defined $col_conf->{align} && $col_conf->{align} eq 'right') {
%         $align_attr = 'right';
%       }
        <td align="<% $align_attr %>">
          <& field.mhtml, object => $object, field_name => $col_conf->{name},
             class_info => $class_info, cache => $cache &>
        </td>
%     }
    </tr>
%   }
  </table>

%   if ($max_results_flag) {
  <div>Showing only first <% $max_inline_results_length %> results</div>
%   }
% }
</div>
</div>

<%init>
use PomCur::WebUtil;

my $st = $c->stash();

my @column_headings = ();
my @rows = ();

my $count = 0;
my $max_results_flag = 0;

my $max_inline_results_length =
  $c->config()->{view_options}{max_inline_results_length} || 100;

my $model_name = $c->req()->param('model');
my $class_info = $c->config()->class_info($model_name)->{$config_name};
my $table = $class_info->{source};

my $context;

if ($is_inline) {
  $context = 'inline_list';
} else {
  $context = 'list';
}

my @column_confs =
  PomCur::WebUtil::get_column_confs($c, $rs, $class_info, $context);

$rs = PomCur::WebUtil::process_rs_options($rs, [@column_confs]);

$st->{column_confs} = [@column_confs];

while (my $obj = $rs->next()) {
  push @rows, $obj;

  if ($is_inline) {
    if ($count++ > $max_inline_results_length) {
      $max_results_flag = 1;
      last;
    }
  }
}

my $disclosed_style = 'undisclosed-title';

if ($disclosed) {
  $disclosed_style = 'disclosed-title';
}

# speed up field.mhtml by caching the database lookups
my $cache = {};
</%init>
