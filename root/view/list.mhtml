<%args>
$rs
$config_name
$list_title
$disclosed => 1  # true will make the list default to visible
</%args>

<div class="sect">
% if ($list_title ne '') {
<span class="sect-title <% $disclosed_style %>"><a href="#"> <% $list_title %> </a> </span>
% }
<div class="sect-content">
% if (@rows == 0) {
    [none]
% } else {
  <table class="list">
    <thead>
      <tr>
%   for my $head_col (@column_headings) {
        <th><span class="field-label"><% ucfirst $head_col %></span></th>
%   }
      <th>&nbsp;</th>
      </tr>
    </thead>
%   for my $object (@rows) {
    <tr>
%     for my $col_conf (@column_confs) {
%       my $align_attr = '';
%       if (defined $col_conf->{format} && $col_conf->{format} eq 'integer' ||
%           defined $col_conf->{align} && $col_conf->{align} eq 'right') {
%         $align_attr = 'right';
%       }
        <td align="<% $align_attr %>">
          <& field.mhtml, object => $object, field_name => $col_conf->{name},
             class_info => $class_info &>
        </td>
%     }
    <td><& field.mhtml, object => $object, field_name => "${table}_id",
           class_info => $class_info &></td>
    </tr>
%   }
  </table>

%   if ($max_results_flag) {
  <div>Showing only first <% $max_inline_results_length %> results</div>
%   }
% }
</div>
</div>

<%init>
use PomCur::WebUtil;

my $st = $c->stash();

my @column_headings = ();
my @rows = ();

my $count = 0;
my $max_results_flag = 0;

my $max_inline_results_length =
  $c->config()->{view_options}{max_inline_results_length} || 100;

my $class_info = $c->config()->class_info($c)->{$config_name};
my $table = $class_info->{source};

my @column_confs =
  PomCur::WebUtil::get_column_confs($c, $rs, $class_info);

$st->{column_confs} = [@column_confs];

for my $col_conf (@column_confs) {
  my $column_name = $col_conf->{name};
  next if $column_name eq 'created_stamp' or $column_name eq "${table}_id";

  my $column_type = $c->schema()->column_type($col_conf, $table);

  next if $column_type eq 'collection';

  my $column_heading = $column_name;
  $column_heading =~ s/_/ /g;

  push @column_headings, ucfirst $column_heading;
}

while (my $obj = $rs->next()) {
  push @rows, $obj;

  if ($count++ > $max_inline_results_length) {
    $max_results_flag = 1;
    last;
  }
}

my $disclosed_style = 'undisclosed-title';

if ($disclosed) {
  $disclosed_style = 'disclosed-title';
}

</%init>
